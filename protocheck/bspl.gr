@@eol_comments :: ?/‌‌#|(//).*?$/?

document = {protocol ~ }+ $;

protocol = type:`protocol` name:spacename '{' ~ 'roles' roles:roles 'parameters' parameters:params (('private' private:params) | ()) references:references '}';

roles = ','.{role} ;
role = name:word ;

params = ','.{param} ;
param = [adornment:adornment] name:word [key:'key'];
adornment = 'out' | 'in' | 'nil' | 'any' | 'opt' ;

references = { message | ref } ;

ref = type:`protocol` name:spacename '(' ~ [roles:roles '|'] params:params ')' ;

message = type:`message` sender:word ('->'|'→'|'↦') ~ recipient:word [':'] name:word '[' parameters:params ']';

word = /[\w-]+/ ;
spacename = /[ \w-]+/ ;